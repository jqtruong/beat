#!/bin/awk -f

@include "init.awk"


BEGIN {
  rate = 44100
  channels = 2
  duration = 0
}


/^; Sample Rate [[:digit:]]+/ {
  rate = $4
}


/^; Channels [[:digit:]]+/ {
  channels = $3
}

/^[^;]/ {
  duration        = $1
  data[s_data, 0] = $1
  data[s_data, 1] = $2
  data[s_data, 2] = $3
  s_data++
}


END {
  print "{"

  printf("%s: %d, %s: %d,",
         q("rate"), rate,
         q("channels"), channels)

  printf("%s: %f,", q("duration"), duration)

  printf("%s: [", q("dat"))
  print_first_frame()
  for (i=1; i<s_data; i++) {
    print_next_frame(i)
  }

  print "]}"
}


function q(str) {
  if (!str) str = "0"
  return sprintf("\"%s\"", str)
}

function print_first_frame() {
  frame = data[0, 0]
  ch1   = data[0, 1]
  ch2   = data[0, 2]
  printf("[%f, %f, %f]", frame, ch1, ch2)
}

function print_next_frame(i) {
  frame = data[i, 0]
  ch1   = data[i, 1]
  ch2   = data[i, 2]
  printf(",[%f, %f, %f]", frame, ch1, ch2)
}
